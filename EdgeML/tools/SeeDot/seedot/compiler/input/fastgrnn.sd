// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT license.


// FastGRNN SeeDot code for usps10 dataset.


let W = (16, 32) in [-1.24, 1.44] in
let U = (32, 32) in [-2.88, 2.15] in
let Bg = (1, 32) in [-1.61, 5.3] in
let Bh = (1, 32) in [-0.14, 2.79] in
let FC = (32, 10) in [-5.18, 4.42] in
let FCbias = (1, 10) in [-1.86, 2.6] in
let X = (256, 1)  in [-2.0, 7.0] in

let XX = reshape(X, (16, 1, 16), (1, 2)) in

let zeta = 0.66544217 in
let nu = 0.07351732 in

let H = init([1, 32], 0.0) in

let res = loop(i = [0:16], H)
(
	let a = (XX[i] * W) in
	let b = (H * U) in
	let c = a + b in
	let g = sigmoid(c + Bg) in
	let h = tanh(c + Bh) in
	let H = (g <*> H) + (zeta * (1.0 - g) + nu) <*> h in H
) in

let score = (res * FC) + FCbias in
argmax(score)
